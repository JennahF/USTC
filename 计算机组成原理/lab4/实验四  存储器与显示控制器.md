# **实验四**  存储器与显示控制器

## 逻辑设计

<img src="F:\COD\lab4\img\1556545816759.png" style="width:500px">

__VRAM:__ 视频存储器

__PCU:__ Paint Control Unit, 绘画控制单元

__DCU:__ Display Control Unit, 显示控制单元

__VRAM:__

<img src="F:\COD\lab4\img\1556612113055.png" style="width:200px">

__DCU:__

<img src="F:\COD\lab4\img\1556613640436.png" style="width:500px">

## 核心代码

__top模块：board_top:__

```verilog
module board_top(
    input [11:0]rgb,//指针在当前位置写入的颜色
    input [3:0]dir,//移动方向，{up,down,left,right}
    input draw,//画笔使能
    input rst,
    input clk_100M,
    
    output [3:0] vga_r, //屏幕显示（r）
    output [3:0] vga_g, //屏幕显示（g）
    output [3:0] vga_b, //屏幕显示（b）
    output hs,      //屏幕显示（横向扫描）
    output vs       //屏幕显示（纵向扫描）
    );
    wire [15:0]paddr;//写存储器的地址
    wire [11:0]pdata;//写存储器的数据
    wire we;
    wire locked;
    wire clk;
    wire [15:0]vaddr;//扫描地址
    wire [11:0]vrgb;//扫描时输出的颜色（给输出赋值）
    wire [11:0]vdata;//扫描时输出的颜色（传给DCU）
    wire [7:0]x;//指针位置横坐标
    wire [7:0]y;//指针位置纵坐标
    assign {vga_r,vga_g,vga_b}=vrgb;
    //调用PCU，更改指针位置，进行绘画
    PCU PCU_DUT(.rgb(rgb),.dir(dir),.draw(draw),.rst(rst),.clk(clk),.x(x),.y(y),.paddr(paddr),.pdata(pdata),.we(we));
    //调用clock的ip核，生成50MHz时钟
    clk_wiz_0 clk_dut(.clk_out1(clk),.reset(rst), .locked(locked), .clk_in1(clk_100M));
    //调用memory的ip核，同步写，异步读。
    dist_mem_gen_0 VRAM_DUT(.a(paddr),.d(pdata),.dpra(vaddr),.we(we),.clk(clk),.dpo(vdata));
    //调用DCU,输出扫描信息
    DCU DCU_DUT(.x(x),.y(y),.pixel_clk(clk),.rst(rst),.vdata(vdata),.vrgb(vrgb),.vaddr(vaddr),.hs(hs),.vs(vs));
    
endmodule
```

__PCU模块：__

```verilog
module PCU(
    input [11:0]rgb,//指针在当前位置写入的颜色
    input [3:0]dir,//移动方向，{up,down,left,right}
    input draw,//画使能
    input rst,
    input clk,
    
    output  [7:0]x,//光标位置横坐标
    output  [7:0]y,//光标位置纵坐标
    output reg [15:0]paddr,//写存储器的地址
    output reg [11:0]pdata,//写存储器的数据
    output we//写使能（始终=画使能）
    );
    reg[7:0]y1,y2,x1,x2;
    wire dir_clk0,dir_clk1,dir_clk2,dir_clk3;
    wire [7:0]step0,step1,step2,step3;
    
    //FSM是一个状态机，用来处理移动过快的问题
    //它以dir[i]作为输入（i=0,1,2,3），输出一个脉冲dir_clk和步长step0（step0始终=1）
    FSM l0(.clk(clk),.rst(rst),.dir(dir[0]),.dir_clk0(dir_clk0),.step0(step0));
    FSM l1(.clk(clk),.rst(rst),.dir(dir[1]),.dir_clk0(dir_clk1),.step0(step1));
    FSM l2(.clk(clk),.rst(rst),.dir(dir[2]),.dir_clk0(dir_clk2),.step0(step2));
    FSM l3(.clk(clk),.rst(rst),.dir(dir[3]),.dir_clk0(dir_clk3),.step0(step3));
	
    //根据方向移动计算y，y1与y2为临时变量
    always@(posedge dir_clk0 or posedge rst)begin
        if(rst)y1<=8'd128;//y1初始化为128
        else y1<=y1+step0;//每当dir_clk0的上升沿到来时，y1+1
    end
    always@(posedge dir_clk1 or posedge rst)begin
        if(rst)y2<=8'd0;//y2初始化为0
        else y2<=y2-step1;//每当dir_clk1的上升沿到来时，y2-1
    end
    assign y=y1+y2;//y始终为y1和y2的和
    
    //根据方向移动计算x，x1与x2为临时变量
    always@(posedge dir_clk2 or posedge rst)begin
        if(rst)x1<=8'd128;//x1初始化为128
    	else x1<=x1+step2;//每当dir_clk2的上升沿到来时，x1+1
	end
	always@(posedge dir_clk3 or posedge rst)begin
        if(rst)x2<=8'd0;//x2初始化为0
    	else x2<=x2-step3;//每当dir_clk3的上升沿到来时，x2-1
	end
	assign x=x1+x2;//x始终为x1和x2的和

    assign we = draw;
    always@(posedge clk or posedge rst)begin
        if(rst)begin
            paddr<=0;
            pdata<=0;
        end
        else begin
            paddr <= {x,y};//给存储器的写地址始终为{x,y}，读取时遵从相同规则
            pdata<=rgb;///给存储器的写数据为输入的rgb
        end
    end
endmodule
```

__DCU模块：__

```verilog
module DCU(
    input [7:0]x,
    input [7:0]y,
    input pixel_clk,
    input rst,
    input [11:0]vdata,
    
    output reg [11:0]vrgb,
    output [15:0]vaddr,
    output hs,
    output vs
    );
    //参数设定遵从逻辑设计中DCU的图
    //以有效区域的左上角为H和V的起始，以便读取存储器
    parameter H_Total = 1040;
    parameter H_Sync_start=585;
    parameter H_Sync_end = 704;
    parameter H_Start1 = 0;
    parameter H_End1 = 584;
    parameter H_Start2 = 704;
    parameter H_End2 = 1040;
    parameter H_board_start=0;
    parameter H_board_end=256;
    
    parameter V_Total = 666;
    parameter V_Sync_start=466;
    parameter V_Sync_end = 471;
    parameter V_Start1 = 0;
    parameter V_End1 = 465;
    parameter V_Start2 = 472;
    parameter V_End2 = 666;
    parameter V_board_start=0;
    parameter V_board_end=256;
    
    //行信号计数器
    reg [11:0] x_cnt;
    always @(posedge pixel_clk or posedge rst)
    begin
        if(rst)
            x_cnt <= 12'd1;
        else if(x_cnt == H_Total)
            x_cnt <= 12'd1;
        else
            x_cnt <= x_cnt + 1'b1;
    end
    
    //根据时序图参数，产生行同步信号
    reg hsync_r;
    always @(posedge pixel_clk or posedge rst)
    begin
        if(rst)
            hsync_r <= 1'b1;
        else if(x_cnt>=H_Sync_start && x_cnt < H_Sync_end)
            hsync_r <= 1'b0;
        else
            hsync_r <= 1'b1;
    end
    
    //行信号有效，即当前数据有效
    reg hs_de;
    always @(posedge pixel_clk or posedge rst)
    begin
        if(rst)
            hs_de <= 1'b0;
        else if(x_cnt>=H_board_start && x_cnt <= H_board_end)
            hs_de <= 1'b1;
        else
            hs_de <= 1'b0;
    end
    
    //场信号计数器
    reg [11:0] y_cnt;
    always @(posedge pixel_clk or posedge rst)
    begin
        if(rst)
            y_cnt <= 12'd1;
        else begin
            if(x_cnt==H_Total)begin
                if(y_cnt==V_Total)y_cnt <= 12'd1;
                else y_cnt <= y_cnt + 1'b1;
            end
        end
    end
    
    //根据时序图参数，产生场同步信号
    reg vsync_r;
    always @(posedge pixel_clk or posedge rst)
    begin
        if(rst)
            vsync_r <= 1'b1;
        else if(y_cnt>=V_Sync_start && y_cnt<V_Sync_end)
            vsync_r <= 1'b0;
        else
            vsync_r <= 1'b1;
    end
    
    //列信号有效，即当前数据有效
    reg vs_de;
    always @(posedge pixel_clk or posedge rst)
    begin
        if(rst)
            vs_de <= 1'b0;
        else if(y_cnt>=V_board_start && y_cnt <= V_board_end)
            vs_de <= 1'b1;
        else
            vs_de <= 1'b0;
    end
    
    reg mouse_en;//光标使能
    always@(posedge pixel_clk or posedge rst)begin
        if(rst)mouse_en<=0;
        else begin//若扫描到的当前地址在(x,y)上下左右中任一位，的光表示能为1，输出显示为黑色
            if((x_cnt==x&&y_cnt==y)||(x_cnt==x-1&&y_cnt==y)||(x_cnt==x+1&&y_cnt==y)||(x_cnt==x&&y_cnt==y-1)||(x_cnt==x&&y_cnt==y+1))mouse_en=1;
            else mouse_en<=0;
        end
    end
    wire vga_de;
    assign hs = hsync_r;
    assign vs = vsync_r;
    assign vga_de = hs_de & vs_de;  //只有当行信号和场信号同时有效时数据才有效
    
    always@(posedge pixel_clk or posedge rst)begin
        if(rst)vrgb<=12'b0;
        else begin
            if(hs_de & vs_de)begin//若行信号与场信号均有效时，扫描输出为从存储器读到的vdata
                if(mouse_en)vrgb<=12'h0;
                else vrgb<=vdata;
            end
            else vrgb<=12'b0;
        end
    end
    
    wire [7:0]x_read;
    wire [7:0]y_read;
    assign x_read = x_cnt;
    assign y_read = y_cnt;
    assign vaddr = {x_read,y_read};//当前扫描地址为{x_cnt,y_cnt}，传给存储器读数据
    
endmodule
```

__FSM模块：__

```verilog
module FSM(
    input clk,//50MHz
    input rst,
    input dir,//输入的方向控制信号
    output reg dir_clk0,//输出脉冲
    output [7:0]step0
    );
    parameter s = 8'd1;
    parameter S0=3'd0;
    parameter S1=3'd1;
    parameter S2=3'd2;
    parameter S3=3'd3;
    parameter S4=3'd4;
    reg [29:0]cnt0;
    reg [2:0]cur_state0;
    reg [2:0]next_state0;
    wire hold0;
    
    always@(posedge clk or posedge rst)begin//cur_state0
        if(rst)cur_state0<=S0;
        else cur_state0<=next_state0;
    end
    
    always@(*)begin//next_state0
            case(cur_state0)
                S0:begin
                    if(dir)next_state0=S1;
                    else next_state0=S0;
                end
                S1:begin next_state0<=S2;end
                S2:begin
                    if(!dir)next_state0=S0;
                    else if(dir&&!hold0)next_state0=S2;
                    else if(dir&&hold0)next_state0=S3;
                end
                S3:begin next_state0=S4;end
                S4:begin
                    if(!dir)next_state0=S0;
                    else if(dir&&hold0)next_state0=S3;
                    else if(dir&&!hold0)next_state0=S4;
                end
                default:next_state0=cur_state0;
            endcase
        end
        
            always@(posedge clk or posedge rst)begin//cnt0
            if(rst)begin cnt0<=0;end 
            else begin
                if(dir)begin if(cnt0==30'd5000000)cnt0<=0;else cnt0<=cnt0+1;end
                else cnt0<=0;
            end
        end
    
    assign hold0=(cnt0==30'd5000000);//cnt0为5000000时说明过了1s，hold0置为1
        always@(*)begin//dir_clk0
        case(cur_state0)
            S0:dir_clk0=0;
            S1:dir_clk0=1;
            S2:dir_clk0=0;
            S3:dir_clk0=1;
            S4:dir_clk0=0;
            default:dir_clk0=0;
        endcase
    end
        
    assign step0=8'b1;
endmodule
```

__FSM状态图：__

<img src="F:\COD\lab4\img\fsm.png" style="width:700px">

## 下载

<img src="F:\COD\lab4\img\微信图片_20190505163858.jpg" style="width:500px">

忘记带一卡通了（）

## 实验总结

在本次试验中，我学习了屏幕的扫描原理并利用存储器实现了自定义画板。整个写代码的过程还算流畅，只要把各个功能分配给各个部件执行再在top模块中进行数据传输就行。但最后的时候我遇到了一个问题始终解决不了：

本来在PCU中，我更新x和y的代码如下：

```verilog
    always@(posedge dir_clk0 or posedge dir_clk1 or posedge rst)begin//y
        if(rst)begin
             y<=8'd128;
        end
        else begin
            if(dir_clk0)y<=y-step0;
            else;
            if(dir_clk1)y<=y+step1;
            else;
        end
    end

    always@(posedge dir_clk2 or posedge dir_clk3 or posedge rst)begin//x
        if(rst)begin
             x<=8'd128;
        end
        else begin
            if(dir_clk2)x<=x-step2;
            else;
            if(dir_clk3)x<=x+step3;
            else;
        end
    end
```

即，我在always块里用到了判断上升沿的时钟的值（6,8,18,20行）。

这样的代码下载后我发现，右键和下键是没有问题的，但是左键和上键没有反应。但是具体为什么不能这么写我还没有弄清楚。



